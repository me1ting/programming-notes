# 堆内存和分配器

## alloc和free

`alloc`用于分配切片，free用于释放切片。

```go
const std = @import("std");

pub fn main() !void {
	// we'll be talking about allocators shortly
	var gpa = std.heap.GeneralPurposeAllocator(.{}){};
	const allocator = gpa.allocator();

	// ** The next two lines are the important ones **
	var arr = try allocator.alloc(usize, try getRandomCount());
	defer allocator.free(arr);

	for (0..arr.len) |i| {
		arr[i] = i;
	}
	std.debug.print("{any}\n", .{arr});
}

fn getRandomCount() !u8 {
	var seed: u64 = undefined;
	try std.posix.getrandom(std.mem.asBytes(&seed));
	var random = std.Random.DefaultPrng.init(seed);
	return random.random().uintAtMost(u8, 5) + 5;
}
```

`alloc`可能会出现`OutOfMemory`错误（中文称`内存溢出`，或者`内存不足`），因此这里使用`try`。

使用`defer`在函数退出时释放内存，也称为`try alloc + defer free`模式。

## defer和errordefer

defer 在退出作用域时执行给定的代码。

Zig 的 defer 类似于 Go 的 defer，但存在一个主要区别。在 Zig 中，defer 将在其包含作用域的末尾运行。在 Go 中，defer 是在包含函数的末尾运行。

defererror 只在返回错误时执行，常见场景包括：如果因为出错而不得不撤销之前的分配。

struct使用内存分配的一个常见模式：在`init()`中分配内存，在`deinit()`中释放内存。

```go
const std = @import("std");
const Allocator = std.mem.Allocator;

pub const Game = struct {
	players: []Player,
	history: []Move,
	allocator: Allocator,

	fn init(allocator: Allocator, player_count: usize) !Game {
		var players = try allocator.alloc(Player, player_count);
		errdefer allocator.free(players);

		// store 10 most recent moves per player
		var history = try allocator.alloc(Move, player_count * 10);

		return .{
			.players = players,
			.history = history,
			.allocator = allocator,
		};
	}

	fn deinit(game: Game) void {
		const allocator = game.allocator;
		allocator.free(game.players);
		allocator.free(game.history);
	}
};
```

这种模式在C++中也称为`RAII`，资源获取即初始化，它强调：

- 使用类封装对于堆内存的使用
- 必须且只能在初始化时对堆内存进行分配，在析构时对堆内存进行释放

与C++不同，Zig并非在语法上强制对这两个函数进行限制，甚至这两个名字也只是社区约定。在某些情况下，包括在标准库中，会使用 open 和 close，或其他更适当的名称。

Zig并没有对`deinit`方法何时调用进行语言上的规定。

## 双重释放和内存泄漏

`free`在一个堆内存上错误的调用第二次，称为`双重释放`。这会导致程序Bug。

内存泄露指`alloc`的堆内存没有正确`free`，当程序长时间执行时，会耗尽内存。

## create 和 destroy

如果是创建特定类型的值而非序列，`std.mem.Allocator`提供了create 和 destroy函数。

```go
const std = @import("std");

pub fn main() !void {
	// again, we'll talk about allocators soon!
	var gpa = std.heap.GeneralPurposeAllocator(.{}){};
	const allocator = gpa.allocator();

	// create a User on the heap
	var user = try allocator.create(User);

	// free the memory allocated for the user at the end of this scope
	defer allocator.destroy(user);

	user.id = 1;
	user.power = 100;

	// this line has been added
	levelUp(user);
	std.debug.print("User {d} has power of {d}\n", .{user.id, user.power});
}

fn levelUp(user: *User) void {
	user.power += 1;
}

pub const User = struct {
	id: u64,
	power: i32,
};
```

`create` 方法接受一个参数，类型`(T)`。它返回指向该类型的指针或一个错误，即 `!*T`。

分配得到的user初始是未定义的，即`undefined`。

### 解引用

```go
fn init(allocator: std.mem.Allocator, id: u64, power: i32) !*User{
	const user = try allocator.create(User);
	user.* = .{
		.id = id,
		.power = power,
	};
	return user;
}
```

`user.*`是对指针进行解引用，这区别于传统的`*user`语法。

## 分配器

Zig 的核心原则之一是无隐藏内存分配。

一般使用两种模式：

- 每次函数/方法调用需要传递分配器
- 使用`init`传递分配器，由对象内部使用

### 通用分配器

`std.heap.GeneralPurposeAllocator`是一个通用的线程安全的分配器。

```go
const std = @import("std");
const httpz = @import("httpz");

pub fn main() !void {
	// create our general purpose allocator
	var gpa = std.heap.GeneralPurposeAllocator(.{}){};

	// get an std.mem.Allocator from it
	const allocator = gpa.allocator();

	// pass our allocator to functions and libraries that require it
	var server = try httpz.Server().init(allocator, .{.port = 5882});

	var router = server.router();
	router.get("/api/user/:id", getUser);

	// blocks the current thread
	try server.listen();
}
```

`GeneralPurposeAllocator(.{}){}`语法有些怪异，它实际上等价于：

```go
const T = std.heap.GeneralPurposeAllocator(.{});//返回一个类型
var gpa = T{};//得到一个类型的实例
```

参数`.{}`是一个隐式类型的结构体初始化器，省略了类型`std.heap.general_purpose_allocator.Config`。

> 这不是我们在“语言概述1”中提到的`匿名结构体`，而是`类型推导`。

### 测试分配器

例如一个`IntList`实现：

```go
pub const IntList = struct {
	pos: usize,
	items: []i64,
	allocator: Allocator,

	fn init(allocator: Allocator) !IntList {
		return .{
			.pos = 0,
			.allocator = allocator,
			.items = try allocator.alloc(i64, 4),
		};
	}

	fn deinit(self: IntList) void {
		self.allocator.free(self.items);
	}

	fn add(self: *IntList, value: i64) !void {
		const pos = self.pos;
		const len = self.items.len;

		if (pos == len) {
			// we've run out of space
			// create a new slice that's twice as large
			var larger = try self.allocator.alloc(i64, len * 2);

			// copy the items we previously added to our new space
			@memcpy(larger[0..len], self.items);

			self.items = larger;
		}

		self.items[pos] = value;
		self.pos = pos + 1;
	}
};
```

在这个例子中，我们看到`@memcpy`内置函数，它可以对内存进行复制。

在调用`add`方法时，当`items`满了需要扩容，我们忘记了对旧的`items`进行释放，这导致了内存泄露，但是程序无法发现。

使用测试分配器，可以发现大部分内存泄露问题：

```go
const testing = std.testing;
test "IntList: add" {
	// We're using testing.allocator here!
	var list = try IntList.init(testing.allocator);
	defer list.deinit();

	for (0..5) |i| {
		try list.add(@intCast(i+10));
	}

	try testing.expectEqual(@as(usize, 5), list.pos);
	try testing.expectEqual(@as(i64, 10), list.items[0]);
	try testing.expectEqual(@as(i64, 11), list.items[1]);
	try testing.expectEqual(@as(i64, 12), list.items[2]);
	try testing.expectEqual(@as(i64, 13), list.items[3]);
	try testing.expectEqual(@as(i64, 14), list.items[4]);
}
```

测试器会报错，并准确提示内存泄露位置：

```bash
Test [1/1] test.IntList: add... [gpa] (err): memory address 0x101154000 leaked:
/code/zig/learning.zig:26:32: 0x100f707b7 in init (test)
   .items = try allocator.alloc(i64, 2),
                               ^
/code/zig/learning.zig:55:29: 0x100f711df in test.IntList: add (test)
 var list = try IntList.init(testing.allocator);

... MORE STACK INFO ...

[gpa] (err): memory address 0x101184000 leaked:
/code/test/learning.zig:40:41: 0x100f70c73 in add (test)
   var larger = try self.allocator.alloc(i64, len * 2);
                                        ^
/code/test/learning.zig:59:15: 0x100f7130f in test.IntList: add (test)
  try list.add(@intCast(i+10));
```

以上代码用到了`@intCast`，它依赖类型推断，将源整数类型强制转换到目标整数类型。`@as`也是强制类型转换，只是指定了具体类型。

### 区域分配器（竞技场分配器）

```go
fn parse(allocator: Allocator, input: []const u8) !Something {
	// create an ArenaAllocator from the supplied allocator
	var arena = std.heap.ArenaAllocator.init(allocator);

	// this will free anything created from this arena
	defer arena.deinit();

	// create an std.mem.Allocator from the arena, this will be
	// the allocator we'll use internally
	const aa = arena.allocator();

	const state = State{
		// we're using aa here!
		.buf = try aa.alloc(u8, 512),

		// we're using aa here!
		.nesting = try aa.alloc(NestType, 10),
	};

	// we're passing aa here, so any we're guaranteed that
	// any other allocation will be in our arena
	return parseInternal(aa, state, input);
}
```

区域分配器会一次性释放由其分配的所有堆内存。

### 固定缓冲区分配器

固定缓冲区分配器在一个长度固定的缓冲区(即切片)上进行内存分配，按照栈的方式进行内存分配和释放，free 和 destroy 只对最后分配/创建的项目有效（想想堆栈）。

```go
const std = @import("std");

pub fn main() !void {
	var buf: [150]u8 = undefined;
	var fa = std.heap.FixedBufferAllocator.init(&buf);

	// this will free all memory allocate with this allocator
	defer fa.reset();

	const allocator = fa.allocator();

	const json = try std.json.stringifyAlloc(allocator, .{
		.this_is = "an anonymous struct",
		.above = true,
		.last_param = "are options",
	}, .{.whitespace = .indent_2});

	// We can free this allocation, but since we know that our allocator is
	// a FixedBufferAllocator, we can rely on the above `defer fa.reset()`
	defer allocator.free(json);

	std.debug.print("{s}\n", .{json});
}
```

## 缓存区

接受内存分配器是一种API风格，这要求用户手动释放所分配的内存。

一种避免使用动态内存分配的方式是提供接受缓冲区的API：

```go
const std = @import("std");

pub fn main() !void {
	const name = "Leto";

	var buf: [100]u8 = undefined;
	const greeting = try std.fmt.bufPrint(&buf, "Hello {s}", .{name});

	std.debug.print("{s}\n", .{greeting});
}
```

## writer

另一种避免使用动态内存分配的方式是提供接收 std.io.Writer的API：

```go
const std = @import("std");

pub fn main() !void {
	const out = std.io.getStdOut();

	try std.json.stringify(.{
		.this_is = "an anonymous struct",
		.above = true,
		.last_param = "are options",
	}, .{.whitespace = .indent_2}, out.writer());
}
```

习惯上Allocator通常是函数的第一个参数，而 Writer通常是最后一个参数。
