# 语言概述1

```go
const std = @import("std");

// 如果 `main` 不是 `pub` (public)，此代码将无法编译
pub fn main() void {
	const user = User{
		.power = 9001,
		.name = "Goku",
	};

	std.debug.print("{s}'s power is {d}\n", .{user.name, user.power});
}

pub const User = struct {
	power: u64,
	name: []const u8,
};
```

## 模块

Zig目前的模块系统比较简陋：

- 使用`@import`导入外部模块名，除了标准库使用`std`，其它使用文件名
- 使用`pub`暴露模块的元素

> `@`开头，表示这是一个内置函数

```go
// models/user.zig
pub const MAX_POWER = 100_000;

pub const User = struct {
	power: u64,
	name: []const u8,
};

// main.zig
// 导入值
const User = @import("models/user.zig").User;
// 导入模块
const user = @import("models/user.zig");
const User = user.User;
const MAX_POWER = user.MAX_POWER;
```

这简陋得和早期Go语言很像。

## 代码注释

支持以下注释：

- `//`，普通注释
- `//!` 顶级文档注释
- `///`，文档注释

Zig故意[不支持](https://github.com/ziglang/zig/issues/161)`/*...*/`，有其理由，但是确实在一些场景不太方面。

## 函数

每个可执行文件的入口是`pub main`函数，通常返回`void`。

```go
const std = @import("std");

pub fn main() void {
	const sum = add(8999, 2);
	std.debug.print("8999 + 2 = {d}\n", .{sum});
}

fn add(a: i64, b: i64) i64 {
	return a + b;
}
```

函数的参数是**隐式const**的，这是Zig的一个重要概念。

```go
fn add(a: i64, b: i64) i64 {
	a += b;// 错误，不能修改a
	return a;
}
```

## 数字类型

常用的数字类型包括：u8, i8, u16, i16, u32, i32, u47, i47, u64, i64, f16, f32, f64,f80,f128, 其中：

- `u47`,`i47`是一类`非常规整数类型`：Zig支持任意宽度的整数类型
- 而`f16`,`f80`是`非常规浮点数类型`，但不像整数，非常规数字类型只有这两种

> 从实现来讲，宽度小于64的整数类型都可以用宽度比他大的常规整数类型来模拟，而宽度大于64的整数类型，可以使用数据结构封装。
> 同理，`f16`可以用`f32`来模拟，`f80`使用`f128`来模拟。此外，不排除特殊的硬件平台原生支持这两个类型的可能性。

类型`usize`是一种无符号数字类型，用于表示平台位宽度，因此用来表示长度、大小，如数组长度。

## 结构体

定义结构体类型：

```go
pub const User = struct {
	power: u64,
	name: []const u8,
};
```

可以指定默认值：

```go
pub const User = struct {
	power: u64 = 0,
	name: []const u8,
};
```

创建结构体值：

```go
const user:User = User{.name = "Goku"};// 语法风格追求“创新”

const user = User{.name = "Goku"};//类型推导，可以省略user的类型声明
const user: User = .{};//类型推导，可以省略值的类型声明
```

结构体可以作为命名空间，包含各种声明：常量、方法、函数。

```go
pub const User = struct {
	power: u64 = 0,
	name: []const u8,

	pub const SUPER_POWER = 9000;

	pub fn diagnose(user: User) void {
		if (user.power >= SUPER_POWER) {
			std.debug.print("it's over {d}!!!", .{SUPER_POWER});
		}
	}

	pub fn init(name: []const u8, power: u64) User {
		return User{
			.name = name,
			.power = power,
		};
	}
};
```

方法就是首个参数为其自身或自身指针的函数。

类似Go与Rust，Zig没有什么`构造函数`魔法，习惯上使用`init`函数来初始化结构体。

## const与var

`const`用于声明不可变的量，而`var`用于声明可变的量。

## 数组和切片

与Go一样，数组是固定长度且编译时确定。切片是指向数组中数据的指针和一个长度信息的封装。

```go
const a = [5]i32{1, 2, 3, 4, 5};

// 我们已经在结构体中使用过 .{...} 语法，
// 它也适用于数组

const b: [5]i32 = .{1, 2, 3, 4, 5};

// 使用 _ 让编译器推导长度
const c = [_]i32{1, 2, 3, 4, 5};
```

如果长度信息在编译时已知，切片实际得到指向一个数组的指针。但这并不是问题，因为在Zig中，指向数组的指针将被强制转换为切片。

```go
const a = [_]i32{1, 2, 3, 4, 5};
const b = a[1..4];//数组指针，类型为*const [3]i32

var end: usize = 3;
end += 1;
const c = a[1..end];//切片，类型为[]const i32

const d = a[2..];// 省略end的切片
```

## 字符串

```go
const user = User{.name = "Goku"};
```

字符串是字节类型`u8`的序列（数组或切片），通常基于UTF-8编码。类似C，字符串字母值是基于常量池存储的，并且以空值`\0`表示结尾。

Zig使用`[LENGTH:SENTINEL]`语法表示带末尾值的数组类型，`SENTINEL`表示末尾额外的特殊值，`"Guko"`的类型是`*const[4:0]u8`数组类型，被赋值给了`[]const u8`切片类型。

这经历了自动类型转化，将字符串字面量类型转化成了切片类型，在Zig上下文中，我们指的字符串，通常指`[]const u8`，其同时也可以表示任意二进制数据。

简单来说，Zig是一个比较底层的编程语言，其目前没有`string`类型。

对于编译时未知的字符串值，虽然指向动态内存，我们还是使用`[]const u8`，来强调字符串类型的**不可变性**。

## comptime 和 anytype

```go
std.debug.print("{s}'s power is {d}\n", .{user.name, user.power});
```

编译时执行（comptime）是 Zig 元编程功能的核心，在编译时执行代码而非运行时。

`print`函数涉及到comptime的概念，print函数要求第一个参数是编译时已知的：

```go
pub fn print(comptime fmt: []const u8, args: anytype) void {
```

类似Go，整数和浮点数在编译时(即字面值)都有一个无限精度的默认类型`comptime_int` 和 `comptime_float`。

Zig 会为传递给它的所有`args`类型专门创建一个单独的 `print` 函数。

`.{}`语法创建的是一个`匿名结构体`：

```go
pub fn main() void {
	std.debug.print("{any}\n", .{@TypeOf(.{.year = 2023, .month = 8})});
	std.debug.print("{any}\n", .{@TypeOf(.{ 2023, 8 })});
}
// out:
// struct{comptime year: comptime_int = 2023, comptime month: comptime_int = 8}
// struct{comptime comptime_int = 2023, comptime comptime_int = 8}
```

没有指定字段名称的匿名结构体，称为`元组`。

Zig 没有函数重载，也没有可变函数，但是 comptime 使得它同样能实现相同的效果。
